grammar org.lilychant.LilyChant hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate lilyChantScript "http://www.lilychant.org/script/LilyChantScript"

Script:
	(
		(tones+=Tone)+
//		ensemble=Ensemble
		(chants+=Chant)*
	)?
;

/*
 * Tone definitions
 */

Tone:
	'Tone' name=ID 'in' key=ID ':'
		BEGIN
		'Voices' ':' voiceNames+=VoiceName+
		(phrases+=TonePhrase)+
		END
;

VoiceName:
	name=ID
;

TonePhrase:
	'Phrase' name=ID ':'
		BEGIN
		voices+=VoicePhrase+
		END
;

VoicePhrase:
	'Voice' name=[VoiceName] ':'
		BEGIN
		notes+=Note+
		END
;


///*
// * Ensemble
// */
//
//Ensemble:
//	'ensemble' ':'
//	'top' 'staff' ':' BEGIN voices+=[VoiceName]+ END
//	'bottom' 'staff' ':' BEGIN voices+=[VoiceName]+ END
//;

/*
 * Chants
 */

Chant:
	'Chant' 'in' 'Tone' toneName=[Tone] 'in' key=ID ':'
		BEGIN
		phrases+=LyricPhrase
		('|' phrases+=LyricPhrase)*
		END
;

LyricPhrase:
	noteGroups+=NoteGroup ('.' noteGroups+=NoteGroup)*
;

NoteGroup:
	syllabels+=(ID|HYPHEN|EXTENDER)+
;


/*
 * Notes and keys
 */

//enum Mode:
//	major='major' | minor='minor'
//;

Note:
	ID INT?
;

/*
 * Terminals
 */

terminal ID: ('a'..'z'|'A'..'Z')*;
terminal INT: ('0'..'9')+;
terminal HYPHEN: '--';
terminal EXTENDER: '__';

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';

terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS : (' '|'\t'|'\r'|'\n')+;
