grammar org.lilychant.LilyChant hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate lilyChantScript "http://www.lilychant.org/script/LilyChantScript"

Script:
	(
		(tones+=Tone)+
//		ensemble=Ensemble
		(chants+=Chant)*
	)?
;

/*
 * Tone definitions
 */

// TODO VOICE NAMES SHOULD BE GLOBAL

Tone:
	'Tone' name=ID 'in' key=ID
		BEGIN
		'Voices'
			BEGIN
			voiceNames+=VoiceName+
			END
		(phrases+=TonePhrase)+
		END
;

VoiceName:
	name=ID
;

TonePhrase:
	'Phrase' name=ID
		BEGIN
		voices+=VoicePhrase+
		END
;

VoicePhrase:
	'Voice' name=[VoiceName]
		BEGIN
		notes+=Note+
		END
;


///*
// * Ensemble
// */
//
//Ensemble:
//	'ensemble' ':'
//	'top' 'staff' ':' BEGIN voices+=[VoiceName]+ END
//	'bottom' 'staff' ':' BEGIN voices+=[VoiceName]+ END
//;

/*
 * Chants
 */

Chant:
	'Chant' 'in' 'Tone' tone=[Tone] 'in' key=ID
		BEGIN
		phrases+=LyricPhrase+
		END
;

LyricPhrase:
	'Phrase' notes=[TonePhrase]
		BEGIN
		noteGroups+=NoteGroup+
		END
;

NoteGroup:
	// TODO Doesn't support hyphens at the beginning of notegroups
	(syllables+=ID syllables+=(HYPHEN|EXTENDER)?)
	| (START_NOTE_GROUP syllables+=(ID|HYPHEN)+ END_NOTE_GROUP)
;


/*
 * Notes and keys
 */

//enum Mode:
//	major='major' | minor='minor'
//;

Note:
	ID DURATION?
;

/*
 * Terminals
 */

terminal ID: '^'?('a'..'z'|'A'..'Z'|','|';'|'.'|':')*;
terminal DURATION: ('1'|'2'|'4'|'8'|'16'|'\breve')+;
terminal HYPHEN: '--';
terminal EXTENDER: '__';
terminal START_NOTE_GROUP: '<';
terminal END_NOTE_GROUP: '>';

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';

terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS : (' '|'\t'|'\r'|'\n')+;