grammar org.lilychant.script.LilyChantScript hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate lilyChantScript "http://www.lilychant.org/script/LilyChantScript"

Script:
	(
		(tones+=Tone)*
		ensemble=Ensemble
		(chants+=Chant)*
	)?
;


/*
 * Tone definitions
 */

Tone:
	'Tone' name=ID 'in' key=Key 'with' 'voices' voiceNames+=VoiceName+ ':'
	BEGIN phrases+=TonePhrase+ END
;

VoiceName:
	name=ID
;

TonePhrase:
	'Phrase' name=ID ':'
	BEGIN voices+=VoicePhrase+ END
;

VoicePhrase:
	'Voice' name=[VoiceName]
	BEGIN notes+=Note+ END
;


/*
 * Ensemble
 */

Ensemble:
	'ensemble' ':'
	'top' 'staff' ':' BEGIN voices+=[VoiceName]+ END
	'bottom' 'staff' ':' BEGIN voices+=[VoiceName]+ END
;


/*
 * Chants
 */

Chant:
	'chant' 'in' 'tone' name=ID 'in' key=Key ':'
	phrases+=LyricPhrase ('|' phrases+=LyricPhrase)*
;

LyricPhrase:
	{Phrase}
	noteGroups+=NoteGroup ('.' noteGroups+=NoteGroup)*
;

NoteGroup:
	syllabels+=(SYLLABEL | HYPHEN | EXTENDER)+
;


/*
 * Notes and keys
 */

Key:
	pitch=Pitch mode=Mode
;

Note:
	pitch=Pitch duration=Duration
;

enum Duration:
	sixteenth='16' | eigth='8' | quarter='4' | half='2' | whole='1'
;

enum Pitch:
	a='a' | a_sharp='ais' | a_flat='aes'
	| b='b' | b_sharp='bis' | b_flat='bes'
	| c='c' | c_sharp='cis' | c_flat='ces'
	| d='d' | d_sharp='dis' | d_flat='des'
	| e='e' | e_sharp='eis' | e_flat='ees'
	| f='f' | f_sharp='fis' | f_flat='fes'
	| g='g' | g_sharp='gis' | g_flat='ges'
;

enum Mode:
	major='major' | minor='minor'
;


/*
 * Terminals
 */

terminal ID: ('a'..'z'|'A'..'Z'|'0'..'9')*;
terminal SYLLABEL: '^'? ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z')* ;
terminal HYPHEN: '--';
terminal EXTENDER: '__';

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';

terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS : (' '|'\t'|'\r'|'\n')+;
